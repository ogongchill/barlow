bootJar.enabled = true
jar.enabled = false

dependencies {
    implementation project(":core:domain")
    implementation project(":core:storage")
    implementation project(":services:notification")
    implementation project(":clients:client-knal-api")
    implementation project(":support:logging")
    implementation project(":support:monitoring")
    implementation project(":support:alert")

    implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation ('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'

    testImplementation 'org.springframework.batch:spring-batch-test'
}

tasks.register('copyResourcesConfig', Copy) {
    from project(':core:storage').file('src/main/resources')
    from project(':core:core-notification').file('src/main/resources')
    from project(':clients:client-knal-api').file('src/main/resources')
    from project(':support:alert').file('src/main/resources')
    from project(':support:monitoring').file('src/main/resources')
    from project(':support:logging').file('src/main/resources')

    into file("$projectDir/build/resources/main")

    include '*.yml'
}

processResources.dependsOn('copyResourcesConfig')

tasks.register('buildZip', Zip) {
    dependsOn('bootJar')

    from compileJava
    from processResources

    into('lib') {
        from(jar)
        from(configurations.runtimeClasspath)
    }
}